%let DownLoadPath=/home/ronnykizito0/sasuser.v94/FOOTBALL/Data/Database;
%let start_date=200801;
%let   end_date=201901;
%put &start_date. &end_date. ;
%let intervall=year;
%let start=%sysfunc(inputn(%sysfunc(inputn(&start_date., yymmn6.), date9.), 
	anydtdte9.));
%let end=%sysfunc(inputn(%sysfunc(inputn(&end_date., yymmn6.), date9.), 
	anydtdte9.));
%let dif=%sysfunc(intck(&intervall., &start., &end.));
option nomlogic nomprint nosource nonotes nosymbolgen;

proc datasets library=work KILL NOLIST;
	run;
	%let ReplaceTeam=HomeTeam AwayTeam;

	%macro Fussball;
		%do j=0 %to &dif;
			%let date=%sysfunc(putn(%sysfunc(intnx(&intervall., &start., &j., b)), 
				date9.));
			%let ar=%sysfunc(inputn(&date., date9.), year2.);
			%let NextYear=%sysfunc(intnx(year, "&date"d, 1, same), year2.);
			%let period=&ar.&NextYear.;
			filename resp "&DownLoadPath./fussball.zip";

		proc http url="https://www.football-data.co.uk/mmz4281/&period./data.zip" 
				method="GET" out=resp;
		run;

		/* unzip the file */
		filename inzip ZIP "&DownLoadPath./fussball.zip";
		filename csv "&DownLoadPath./fussball.csv";

		data _null_;
			/* using member syntax here */
			infile inzip("*.csv") lrecl=256 recfm=F length=length eof=eof unbuf;
			file csv lrecl=256 recfm=N;
			input;
			put _infile_ $varying256. length;
			return;
eof:
			stop;
		run;

		* Read data use proc import to get variable names;
		options obs=2;
		options validvarname=v7;

		PROC IMPORT DATAFILE=csv OUT=temp DBMS=csv REPLACE;
			DELIMITER=",";
			GETNAMES=YES;
		RUN;

		options obs=max;
		run;
		* Put variable names into macro variable;

		PROC CONTENTS DATA=temp out=InVars NOPRINT;
		RUN;

		data InVars;
			set InVars;
			length="50";
		run;

		PROC SQL NOPRINT;
			SELECT CATT(name, ' : $', length, '.') INTO :InvarsMacro SEPARATED BY ' ' 
				FROM InVars ORDER BY varnum;
		QUIT;

		* Read real data;
		%let FileChars=FTHG FTAG;
		%let CharsToNumeric=%qsysfunc(tranwrd(%sysfunc(compbl(x&FileChars.)), 
			%str( ), %bquote( x)));
		%put &CharsToNumeric;

		DATA v1 (keep=Div Date HomeTeam AwayTeam FTHG FTAG FTR FilePeriod);
			INFILE csv DELIMITER="," MISSOVER DSD FIRSTOBS=2 LRECL=32767;
			INPUT &InvarsMacro.;
			FilePeriod="&period.";

			if div ne "Div";
			array StripChars {*} _character_;

			/* this array removes all leading spaces*/
			do _n_=1 to dim(StripChars);
				StripChars{_n_}=strip(StripChars{_n_});
			end;
			NewDateCol=input(catx('-', substr(date, 7), substr(date, 4, 2), substr(date, 
				1, 2)), yymmdd10.);
			drop date;
			rename NewDateCol=Date;
			format NewDateCol yymmdd10.;

			%macro CharsToNumeric;
				%let The_var_list=&FileChars.;
				%let i = %sysfunc(CountW(&The_var_list.));

				%do Variables=1 %to &i.;
					%let Next_Variable=%scan(&The_var_list., &Variables.);

					/*****ex byt till char*/
					C&Next_Variable.=input(&Next_Variable., best12.);
					drop &Next_Variable.;
					rename C&Next_Variable.=&Next_Variable.;
				%end;
			%mend;

			%CharsToNumeric;

			%macro ReplaceTeam;
				%let The_var_list=&ReplaceTeam.;
				%let i = %sysfunc(CountW(&The_var_list.));

				%do Variables=1 %to &i.;
					%let Next_Variable=%scan(&The_var_list., &Variables.);

					if &Next_Variable.="Airdrie" then
						&Next_Variable.="Airdrie Utd";
					else if &Next_Variable.="Leipzig" then
						&Next_Variable.="RB Leipzig";
					else if &Next_Variable.="Red Star 93" then
						&Next_Variable.="Red Star";
					else if &Next_Variable.="Roda" then
						&Next_Variable.="Roda JC";
					else if &Next_Variable.="Sparta" then
						&Next_Variable.="Sparta Rotterdam";
					else if &Next_Variable.="Villareal" then
						&Next_Variable.="Villarreal";
					else if &Next_Variable.="Proodeytiki" then
						&Next_Variable.="Proodeftiki";
					else if &Next_Variable.="Antalya" then
						&Next_Variable.="Antalyaspor";
					else
						&Next_Variable.=&Next_Variable.;
				%end;
			%mend;

			%ReplaceTeam;

			if FTHG >=0;
		RUN;

		data v1;
			retain Div Date HomeTeam AwayTeam FTHG FTAG FTR Utfall PoangHemma PoangBorta;
			set v1;
			Utfall=1;

			if ftr="D" then
				Utfall=0;
			else if ftr="A" then
				Utfall=0;
			PoangHemma=3;

			if ftr="D" then
				PoangHemma=1;
			else if ftr="A" then
				PoangHemma=0;
			PoangBorta=0;

			if ftr="D" then
				PoangBorta=1;
			else if ftr="A" then
				PoangBorta=3;
		run;

		proc append base=Fussball data=v1 force;
		run;

		data _null_;
			rc=fdelete('csv');
			put rc=;
			msg=sysmsg();
			put msg=;
		run;

		data _null_;
			rc=fdelete('inzip');
			put rc=;
			msg=sysmsg();
			put msg=;
		run;

		%put &ar &NextYear &period;
	%end;
%mend;

%Fussball;
